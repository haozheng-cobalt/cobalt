# Copyright 2021 The Cobalt Authors. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//starboard/build/config/os_definitions.gni")

target(gtest_target_type, "nplb") {
  testonly = true

  sources = [
    "//starboard/common/test_main.cc",
    "//starboard/nplb/sabi/alignment_test.cc",
    "//starboard/nplb/sabi/endianness_test.cc",
    "//starboard/nplb/sabi/signedness_and_size_of_enum_test.cc",
    "//starboard/nplb/sabi/signedness_of_char_test.cc",
    "//starboard/nplb/sabi/size_test.cc",
    "//starboard/nplb/sabi/struct_alignment_test.cc",
    "//starboard/shared/starboard/drm/drm_test_helpers.cc",
    "//starboard/shared/starboard/drm/drm_test_helpers.h",
    "//starboard/testing/fake_graphics_context_provider.cc",
    "//starboard/testing/fake_graphics_context_provider.h",
    "posix_compliance/posix_errno_test.cc",
  ]

  configs += [ "//starboard/build/config:starboard_implementation" ]

  deps = [
    "//starboard:starboard_group",
    "//starboard/common",
    "//starboard/nplb/compiler_compliance:cpp14_supported",
    "//starboard/shared/starboard/media:media_util",
    "//starboard/shared/starboard/player:video_dmp",
    "//testing/gmock",
    "//testing/gtest",
  ]

  deps += cobalt_platform_dependencies

  if (is_internal_build) {
    deps += [ "//internal/starboard/private/nplb:nplb_private" ]
  }

  if (sb_enable_cpp17_audit) {
    deps += [ "//starboard/nplb/compiler_compliance:cpp17_supported" ]
  }

  data_deps = [
    "//cobalt/network:copy_ssl_certificates",
    "//starboard/nplb/testdata/file_tests:nplb_file_tests_data",
    "//starboard/shared/starboard/player:player_download_test_data",
    "//third_party/icu:icudata",
  ]

  if (is_clang_16) {
    # signedness_and_size_of_enum_test.cc casts -1 to enum value
    cflags = [ "-Wno-enum-constexpr-conversion" ]
  }

  #  TODO: b/297808555 - Add these tests for all  modular builds.
  if (sb_has_unused_symbol_issue) {
    sources -= []

    deps -= [
      "//starboard/shared/starboard/media:media_util",
      "//starboard/shared/starboard/player:video_dmp",
    ]
  }
}
